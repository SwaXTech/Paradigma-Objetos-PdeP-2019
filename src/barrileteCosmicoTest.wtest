import barrileteCosmico.*

describe "Barrilete Cósmico" {

	var garlicSea
	var silverSea
	var lastToninas
	var goodAirs
	var pHari
	var cumbanchaVolante
	var el114
	var goodAirsASilverSea
	var lastToninasAGarlicSea
	
	fixture{
		garlicSea   = new Localidad(nombre = "Garlic's Sea", equipajeImprescindible = ["Caña de pescar", "Piloto"],                precio= 2500, kmUbicacion = 365)
		silverSea   = new Localidad(nombre = "Silver's Sea", equipajeImprescindible = ["Protector Solar", "Equipo de Buceo"],      precio= 1350, kmUbicacion = 415)
		lastToninas = new Localidad(nombre = "Last Toninas", equipajeImprescindible = ["Vacuna Gripal","Vacuna B","Necronomicon"], precio= 3500, kmUbicacion = 340)
		goodAirs    = new Localidad(nombre = "Good Airs",    equipajeImprescindible = ["Cerveza", "Protector Solar"],              precio= 1500, kmUbicacion = 0)
		
		barrileteCosmico.agregarDestino(garlicSea)
		barrileteCosmico.agregarDestino(silverSea)	
		barrileteCosmico.agregarDestino(lastToninas)	
		barrileteCosmico.agregarDestino(goodAirs)
		cumbanchaVolante = new MedioDeTransporte(costoKm = 25, minutosKm = 0.0017)   // Dije suba
		el114 = new MedioDeTransporte(costoKm = 10, minutosKm = 5)
		barrileteCosmico.agregarMedioDeTransporte(cumbanchaVolante)
		barrileteCosmico.agregarMedioDeTransporte(el114)
		goodAirsASilverSea = new Viaje(localidadInicial = goodAirs, localidadFinal = silverSea, medioDeTransporte= cumbanchaVolante)
		lastToninasAGarlicSea = new Viaje(localidadInicial = lastToninas, localidadFinal = garlicSea, medioDeTransporte= el114)
		pHari = new Usuario(nombre = "Pablo Hari", nombreDeUsuario ="PHari", saldo = 10000, localidadDeOrigen = lastToninas)
		
	}
	
	test "Los destinos más importantes de Barrilete Cósmico son: Garlic's Sea y Last Toninas"{
		assert.equals(barrileteCosmico.destinosMasImportantes(), #{garlicSea, lastToninas})
	}
	
	test "Aplicar un descuento de 25% a Last Toninas, su precio es de $2625" {
		lastToninas.aplicarDescuento(25)
		assert.equals(2625, lastToninas.precio())
	}
	
	test "Aplicar un descuento de 25% a Last Toninas, tiene el certificado de descuento en su equipaje imprescindible" {
		lastToninas.aplicarDescuento(25)
		assert.that(lastToninas.equipajeImprescindible().contains("Certificado de descuento"))
	}
	
	test "Aplicar un descuento del 15% a todos los destinos de Barrilete Cósmico, 
			Garlic´s sea: $2125
			Silver's Sea: $1147.5
			Last Toninas: $2975
			Good Airs: $1275
			Todos deben poseer el certificado de descuento."
		{
			barrileteCosmico.aplicarDescuento(15)
			assert.equals(2125, garlicSea.precio())
			assert.equals(1147.5, silverSea.precio())
			assert.equals(2975, lastToninas.precio())
			assert.equals(1275, goodAirs.precio())
			assert.that(barrileteCosmico.todosLosDestinosPoseenCertificadoDeDescuento())
		}
	
	test "Silver's Sea no es un destino peligroso" {
		assert.notThat(silverSea.esPeligroso())
	}
	
	test "Last Toninas es el único destino peligroso" {
		assert.equals(#{lastToninas}, barrileteCosmico.destinosPeligrosos())
	}
	
	test "Barrilete cósmico es una empresa extrema" {
		assert.that(barrileteCosmico.esEmpresaExtrema())
	}
	
	test "Carta de destinos de Barrilete Cosmico"{
		assert.equals(
			#{"Last Toninas", "Garlic's Sea", "Good Airs", "Silver's Sea"}, 
			barrileteCosmico.cartaDeDestinos()
		)
	
	}
	
	test "Los kilometros de PHari son 440" {
		assert.equals(440, pHari.obtenerKM())
	}
	
	test "PHari vuela a Silver's Sea" {
		//assert.that(pHari.puedeViajar(silverSea))
		var viaje = barrileteCosmico.armarUnViaje(pHari, silverSea);
		pHari.viajarA(viaje)
		assert.that(pHari.realizoViajeA(silverSea))
	}
	
	test "PHari vuela a Silver's Sea y se le descuenta dinero"{
		pHari.volarA(silverSea)
		assert.equals(150, pHari.saldo())
	}
	
	test "PHari intenta volar a Last Toninas, pero no le alcanza el dinero" {
		assert.throwsExceptionLike(
			new VuelosUsuarioException(message = "No se cuenta con saldo suficiente para realizar el vuelo"), 
			{ 
				pHari.volarA(lastToninas)
			}
		)
	}
	
	// TP 2
	
	// Medios de transporte
	
	test "La cumbancha volante tarda 17 minutos entre Nueva York y el Congo Belga (10,260.80 km a una velocidad promedio de 36211,76km/h, lo que hace un km en 0,0017 minutos)" {
		assert.equals(0.0017, cumbanchaVolante.minutosKm())
	}
	
	// Localidades
	test "La distancia entre Good Airs y Silver's Sea es de 415 y viceversa" {
		assert.equals(415, goodAirs.distanciaA(silverSea))
		assert.equals(415, silverSea.distanciaA(goodAirs))
	}
	
	test "La distancia entre Last Toninas y Garlic's Sea es de 25 y viceversa" {
		assert.equals(25, lastToninas.distanciaA(garlicSea))
		assert.equals(25, garlicSea.distanciaA(lastToninas))
	}
	
	test "La distancia entre Last Toninas y Last Toninas es 0" {
		assert.equals(0, lastToninas.distanciaA(lastToninas))
	}
	//viajes
	test "El costo entre Good Airs y Silver's Sea en la cumbancha volante es de $11725" {
		assert.equals(11725 ,goodAirsASilverSea.precioViaje())
	}
	test "El costo entre Last Toninas y Garlic's Sea en el 114 es de $2750" {
		assert.equals(2750 ,lastToninasAGarlicSea.precioViaje())
		
	}
}
